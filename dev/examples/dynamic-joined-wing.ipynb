{
 "cells": [
  {
   "cell_type": "markdown",
   "source": [
    "# Time-Domain Simulation of a Joined-Wing\n",
    "\n",
    "In this example we use the same joined-wing model as used in the [previous example](@ref\n",
    "static-joined-wing), but with the following time varying loads applied at the wingtip:\n",
    " - A piecewise-linear load $F_L$ in the x and y-directions defined as follows:\n",
    "$$\n",
    "F_L(t) = \\begin{cases}\n",
    "    t10^6 \\text{ N} & 0 \\leq t \\leq 0.01 \\\\\n",
    "    (0.02-t)10^6 & 0.01 \\leq t \\leq 0.02 \\\\\n",
    "    0 & \\text{otherwise}\n",
    "\\end{cases}\n",
    "$$\n",
    "- A sinusoidal load $F_S$ applied in the z-direction defined as follows:\n",
    "$$\n",
    "F_S(t) = \\begin{cases}\n",
    "    0 & t \\lt 0 \\\\\n",
    "    5 \\times 10^3 (1-\\cos(\\pi t /0.02)) \\text{ N} & 0 \\leq t \\lt 0.02 \\\\\n",
    "    10^4 \\text{ N} & 0.02 \\leq t\n",
    "\\end{cases}\n",
    "$$\n",
    "\n",
    "We will also use the same compliance and mass matrix for all beams, in order to simplify\n",
    "the problem definition.\n",
    "\n",
    "![](../assets/static-joined-wing-drawing.png)"
   ],
   "metadata": {}
  },
  {
   "outputs": [],
   "cell_type": "code",
   "source": [
    "using GXBeam, LinearAlgebra\n",
    "\n",
    "# Set endpoints of each beam\n",
    "p1 = [0, 0, 0]\n",
    "p2 = [-7.1726, -12, -3.21539]\n",
    "p3 = [7.1726, -12,  3.21539]\n",
    "\n",
    "Cab_1 = [\n",
    "0.5         0.866025  0.0\n",
    "0.836516    -0.482963  0.258819\n",
    "0.224144     -0.12941   -0.965926\n",
    "]\n",
    "\n",
    "Cab_2 = [\n",
    "0.5         0.866025  0.0\n",
    "-0.836516    0.482963 0.258819\n",
    "0.224144    -0.12941   0.965926\n",
    "]\n",
    "\n",
    "# beam 1\n",
    "L_b1 = norm(p1-p2)\n",
    "r_b1 = p2\n",
    "nelem_b1 = 8\n",
    "lengths_b1, xp_b1, xm_b1, Cab_b1 = discretize_beam(L_b1, r_b1, nelem_b1, frame=Cab_1)\n",
    "\n",
    "# beam 2\n",
    "L_b2 = norm(p3-p1)\n",
    "r_b2 = p1\n",
    "nelem_b2 = 8\n",
    "lengths_b2, xp_b2, xm_b2, Cab_b2 = discretize_beam(L_b2, r_b2, nelem_b2, frame=Cab_2)\n",
    "\n",
    "# combine elements and points into one array\n",
    "nelem = nelem_b1 + nelem_b2\n",
    "points = vcat(xp_b1, xp_b2[2:end])\n",
    "start = 1:nelem\n",
    "stop = 2:nelem + 1\n",
    "lengths = vcat(lengths_b1, lengths_b2)\n",
    "midpoints = vcat(xm_b1, xm_b2)\n",
    "Cab = vcat(Cab_b1, Cab_b2)\n",
    "\n",
    "# assign all beams the same compliance and mass matrix\n",
    "compliance = fill(Diagonal([2.93944738387698e-10, 8.42991725049126e-10,\n",
    "    3.38313996669689e-08, 4.69246721094557e-08, 6.79584100559513e-08,\n",
    "    1.37068861370898e-09]), nelem)\n",
    "mass = fill(Diagonal([4.86e-2, 4.86e-2, 4.86e-2, 1.0632465e-2, 2.10195e-4,\n",
    "    1.042227e-2]), nelem)\n",
    "\n",
    "# create assembly\n",
    "assembly = Assembly(points, start, stop;\n",
    "    compliance = compliance,\n",
    "    mass = mass,\n",
    "    frames = Cab,\n",
    "    lengths = lengths,\n",
    "    midpoints = midpoints)\n",
    "\n",
    "F_L = (t) -> begin\n",
    "    if 0.0 <= t < 0.01\n",
    "        1e6*t\n",
    "    elseif 0.01 <= t < 0.02\n",
    "        -1e6*(t-0.02)\n",
    "    else\n",
    "        zero(t)\n",
    "    end\n",
    "end\n",
    "\n",
    "F_S = (t) -> begin\n",
    "    if t < 0.0\n",
    "        zero(t)\n",
    "    elseif 0.0 <= t < 0.02\n",
    "        5e3*(1-cos(pi*t/0.02))\n",
    "    else\n",
    "        1e4\n",
    "    end\n",
    "end\n",
    "\n",
    "# assign boundary conditions and point load\n",
    "prescribed_conditions = (t) -> begin\n",
    "    Dict(\n",
    "        # fixed endpoint on beam 1\n",
    "        1 => PrescribedConditions(ux=0, uy=0, uz=0, theta_x=0, theta_y=0, theta_z=0),\n",
    "        # force applied on point 4\n",
    "        nelem_b1 + 1 => PrescribedConditions(Fx=F_L(t), Fy=F_L(t), Fz=F_S(t)),\n",
    "        # fixed endpoint on last beam\n",
    "        nelem+1 => PrescribedConditions(ux=0, uy=0, uz=0, theta_x=0, theta_y=0, theta_z=0),\n",
    "    )\n",
    "end\n",
    "\n",
    "# time\n",
    "t = range(0, 0.04, length=1001)\n",
    "\n",
    "system, history, converged = time_domain_analysis(assembly, t;\n",
    "    prescribed_conditions=prescribed_conditions)\n",
    "\n",
    "nothing #hide"
   ],
   "metadata": {},
   "execution_count": 1
  },
  {
   "cell_type": "markdown",
   "source": [
    "We can visualize tip displacements and the resultant forces accessing the post-processed\n",
    "results for each time step contained in the variable `history`.  Note that the fore-root\n",
    "and rear-root resultant forces for this case are equal to the external forces/moments,\n",
    "but with opposite sign."
   ],
   "metadata": {}
  },
  {
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "sys:1: RuntimeWarning: More than 20 figures have been opened. Figures created through the pyplot interface (`matplotlib.pyplot.figure`) are retained until explicitly closed and may consume too much memory. (To control this warning, see the rcParam `figure.max_open_warning`).\n"
     ]
    }
   ],
   "cell_type": "code",
   "source": [
    "using Plots\n",
    "pyplot()\n",
    "\n",
    "point = vcat(fill(nelem_b1+1, 6), fill(1, 6))\n",
    "field = [:u, :u, :u, :theta, :theta, :theta, :F, :F, :F, :M, :M, :M]\n",
    "direction = [1, 2, 3, 1, 2, 3, 1, 2, 3, 1, 2, 3]\n",
    "ylabel = [\"\\$u_x\\$ (\\$m\\$)\", \"\\$u_y\\$ (\\$m\\$)\", \"\\$u_z\\$ (\\$m\\$)\",\n",
    "    \"Rodriguez Parameter \\$\\\\theta_x\\$\", \"Rodriguez Parameter \\$\\\\theta_y\\$\",\n",
    "    \"Rodriguez Parameter \\$\\\\theta_z\\$\", \"\\$F_x\\$ at the forewing root (\\$N\\$)\",\n",
    "    \"\\$F_y\\$ at the forewing root (\\$N\\$)\", \"\\$F_z\\$ at the forewing root (\\$N\\$)\",\n",
    "    \"\\$M_x\\$ at the forewing root (\\$Nm\\$)\", \"\\$M_y\\$ at the forewing root (\\$Nm\\$)\",\n",
    "    \"\\$M_z\\$ at the forewing root (\\$N\\$)\"]\n",
    "\n",
    "for i = 1:12\n",
    "    plot(\n",
    "        xlim = (0, 0.04),\n",
    "        xticks = 0:0.01:0.04,\n",
    "        xlabel = \"Time (s)\",\n",
    "        ylabel = ylabel[i],\n",
    "        grid = false,\n",
    "        overwrite_figure=false\n",
    "        )\n",
    "    y = [getproperty(state.points[point[i]], field[i])[direction[i]] for state in history]\n",
    "\n",
    "    if field[i] == :theta\n",
    "        # convert to angle\n",
    "        @. y = 4*atan(y/4)\n",
    "    end\n",
    "\n",
    "    if field[i] == :F || field[i] == :M\n",
    "        y = -y\n",
    "    end\n",
    "\n",
    "    plot!(t, y, label=\"\")\n",
    "    plot!(show=true)\n",
    "end\n",
    "\n",
    "nothing #hide"
   ],
   "metadata": {},
   "execution_count": 2
  },
  {
   "cell_type": "markdown",
   "source": [
    "These graphs are identical to those presented in \"GEBT: A general-purpose nonlinear analysis tool for composite beams\" by Wenbin Yu and Maxwell Blair.\n",
    "\n",
    "We can also visualize the time history of the system using ParaView.  In order to view the small deflections we'll scale all the deflections up by a couple orders of magnitude.  We'll also set the color gradient to match the magnitude of the deflections at each point."
   ],
   "metadata": {}
  },
  {
   "outputs": [],
   "cell_type": "code",
   "source": [
    "airfoil  = [ #FX 60-100 airfoil\n",
    "    0.0000000 0.0000000;\n",
    "    0.0010700 0.0057400;\n",
    "    0.0042800 0.0114400;\n",
    "    0.0096100 0.0177500;\n",
    "    0.0170400 0.0236800;\n",
    "    0.0265300 0.0294800;\n",
    "    0.0380600 0.0352300;\n",
    "    0.0515600 0.0405600;\n",
    "    0.0669900 0.0460900;\n",
    "    0.0842700 0.0508600;\n",
    "    0.1033200 0.0556900;\n",
    "    0.1240800 0.0598900;\n",
    "    0.1464500 0.0640400;\n",
    "    0.1703300 0.0675400;\n",
    "    0.1956200 0.0708100;\n",
    "    0.2222100 0.0733900;\n",
    "    0.2500000 0.0756500;\n",
    "    0.2788600 0.0772000;\n",
    "    0.3086600 0.0783800;\n",
    "    0.3392800 0.0788800;\n",
    "    0.3705900 0.0789800;\n",
    "    0.4024500 0.0784500;\n",
    "    0.4347400 0.0775000;\n",
    "    0.4673000 0.0759600;\n",
    "    0.5000000 0.0740900;\n",
    "    0.5327000 0.0717400;\n",
    "    0.5652600 0.0691100;\n",
    "    0.5975500 0.0660800;\n",
    "    0.6294100 0.0627500;\n",
    "    0.6607200 0.0590500;\n",
    "    0.6913400 0.0551100;\n",
    "    0.7211400 0.0508900;\n",
    "    0.7500000 0.0465200;\n",
    "    0.7777900 0.0420000;\n",
    "    0.8043801 0.0374700;\n",
    "    0.8296700 0.0329800;\n",
    "    0.8535500 0.0286400;\n",
    "    0.8759201 0.0244700;\n",
    "    0.8966800 0.0205300;\n",
    "    0.9157300 0.0168100;\n",
    "    0.9330100 0.0134200;\n",
    "    0.9484400 0.0103500;\n",
    "    0.9619400 0.0076600;\n",
    "    0.9734700 0.0053400;\n",
    "    0.9829600 0.0034100;\n",
    "    0.9903900 0.0019300;\n",
    "    0.9957200 0.0008600;\n",
    "    0.9989300 0.0002300;\n",
    "    1.0000000 0.0000000;\n",
    "    0.9989300 0.0001500;\n",
    "    0.9957200 0.0007000;\n",
    "    0.9903900 0.0015100;\n",
    "    0.9829600 0.00251;\n",
    "    0.9734700 0.00377;\n",
    "    0.9619400 0.00515;\n",
    "    0.9484400 0.00659;\n",
    "    0.9330100 0.00802;\n",
    "    0.9157300 0.00941;\n",
    "    0.8966800 0.01072;\n",
    "    0.8759201 0.01186;\n",
    "    0.8535500 0.0128;\n",
    "    0.8296700 0.01347;\n",
    "    0.8043801 0.01381;\n",
    "    0.7777900 0.01373;\n",
    "    0.7500000 0.01329;\n",
    "    0.7211400 0.01241;\n",
    "    0.6913400 0.01118;\n",
    "    0.6607200 0.00951;\n",
    "    0.6294100 0.00748;\n",
    "    0.5975500 0.00496;\n",
    "    0.5652600 0.00217;\n",
    "    0.532700  -0.00092;\n",
    "    0.500000  -0.00405;\n",
    "    0.467300  -0.00731;\n",
    "    0.434740  -0.01045;\n",
    "    0.402450  -0.01357;\n",
    "    0.370590  -0.01637;\n",
    "    0.339280  -0.01895;\n",
    "    0.308660  -0.021;\n",
    "    0.278860  -0.02275;\n",
    "    0.250000  -0.02389;\n",
    "    0.222210  -0.02475;\n",
    "    0.195620  -0.025;\n",
    "    0.170330  -0.02503;\n",
    "    0.146450  -0.02447;\n",
    "    0.124080  -0.02377;\n",
    "    0.103320  -0.02246;\n",
    "    0.084270  -0.0211;\n",
    "    0.066990  -0.01913;\n",
    "    0.051560  -0.0173;\n",
    "    0.038060  -0.01481;\n",
    "    0.026530  -0.01247;\n",
    "    0.017040  -0.0097;\n",
    "    0.009610  -0.00691;\n",
    "    0.004280  -0.00436;\n",
    "    0.001070  -0.002;\n",
    "    0.0        0.0;\n",
    "]\n",
    "\n",
    "section = zeros(3, size(airfoil, 1))\n",
    "for ic = 1:size(airfoil, 1)\n",
    "    section[1,ic] = airfoil[ic,1] - 0.5\n",
    "    section[2,ic] = 0\n",
    "    section[3,ic] = airfoil[ic,2]\n",
    "end\n",
    "\n",
    "write_vtk(\"dynamic-joined-wing-simulation\", assembly, history, t, scaling=1e2;\n",
    "    sections = section)"
   ],
   "metadata": {},
   "execution_count": 3
  },
  {
   "cell_type": "markdown",
   "source": [
    "![](../assets/dynamic-joined-wing-simulation.gif)"
   ],
   "metadata": {}
  },
  {
   "cell_type": "markdown",
   "source": [
    "---\n",
    "\n",
    "*This notebook was generated using [Literate.jl](https://github.com/fredrikekre/Literate.jl).*"
   ],
   "metadata": {}
  }
 ],
 "nbformat_minor": 3,
 "metadata": {
  "language_info": {
   "file_extension": ".jl",
   "mimetype": "application/julia",
   "name": "julia",
   "version": "1.6.5"
  },
  "kernelspec": {
   "name": "julia-1.6",
   "display_name": "Julia 1.6.5",
   "language": "julia"
  }
 },
 "nbformat": 4
}
