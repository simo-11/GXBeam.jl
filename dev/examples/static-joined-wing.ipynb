{
 "cells": [
  {
   "cell_type": "markdown",
   "source": [
    "# Static Analysis of a Joined-Wing\n",
    "\n",
    "In this example we consider the joined-wing model proposed by Blair in \"An Equivalent\n",
    "Beam Formulation for Joined-Wings in a Post-Buckled State\" and optimized by Green\n",
    "et al. in \"Structural Optimization of Joined-Wing Beam Model with Bend-Twist\n",
    "Coupling using Equivalent Static Loads\".\n",
    "\n",
    "![](../assets/static-joined-wing-drawing.png)"
   ],
   "metadata": {}
  },
  {
   "outputs": [],
   "cell_type": "code",
   "source": [
    "using GXBeam, LinearAlgebra\n",
    "\n",
    "# Set endpoints of each beam\n",
    "p1 = [-7.1726, -12, -3.21539]\n",
    "p2 = [-5.37945, -9, -2.41154]\n",
    "p3 = [-3.5863, -6, -1.6077]\n",
    "p4 = [-1.79315, -3, -0.803848]\n",
    "p5 = [0, 0, 0]\n",
    "p6 = [7.1726, -12, 3.21539]\n",
    "\n",
    "# get transformation matrix for left beams\n",
    "\n",
    "# transformation from intermediate frame to global frame\n",
    "tmp1 = sqrt(p1[1]^2 + p1[2]^2)\n",
    "c1, s1 = -p1[1]/tmp1, -p1[2]/tmp1\n",
    "rot1 = [c1 -s1 0; s1 c1 0; 0 0 1]\n",
    "\n",
    "# transformation from local beam frame to intermediate frame\n",
    "tmp2 = sqrt(p1[1]^2 + p1[2]^2 + p1[3]^2)\n",
    "c2, s2 = tmp1/tmp2, -p1[3]/tmp2\n",
    "rot2 = [c2 0 -s2; 0 1 0; s2 0 c2]\n",
    "\n",
    "Cab_1 = rot1*rot2\n",
    "\n",
    "# get transformation matrix for right beam\n",
    "\n",
    "# transformation from intermediate frame to global frame\n",
    "tmp1 = sqrt(p6[1]^2 + p6[2]^2)\n",
    "c1, s1 = p6[1]/tmp1, p6[2]/tmp1\n",
    "rot1 = [c1 -s1 0; s1 c1 0; 0 0 1]\n",
    "\n",
    "# transformation from local beam frame to intermediate frame\n",
    "tmp2 = sqrt(p6[1]^2 + p6[2]^2 + p6[3]^2)\n",
    "c2, s2 = tmp1/tmp2, p6[3]/tmp2\n",
    "rot2 = [c2 0 -s2; 0 1 0; s2 0 c2]\n",
    "\n",
    "Cab_2 = rot1*rot2\n",
    "\n",
    "# beam 1\n",
    "L_b1 = norm(p2-p1)\n",
    "r_b1 = p1\n",
    "nelem_b1 = 5\n",
    "lengths_b1, xp_b1, xm_b1, Cab_b1 = discretize_beam(L_b1, r_b1, nelem_b1;\n",
    "    frame = Cab_1)\n",
    "compliance_b1 = fill(Diagonal([1.05204e-9, 3.19659e-9, 2.13106e-8, 1.15475e-7,\n",
    "    1.52885e-7, 7.1672e-9]), nelem_b1)\n",
    "\n",
    "# beam 2\n",
    "L_b2 = norm(p3-p2)\n",
    "r_b2 = p2\n",
    "nelem_b2 = 5\n",
    "lengths_b2, xp_b2, xm_b2, Cab_b2 = discretize_beam(L_b2, r_b2, nelem_b2;\n",
    "    frame = Cab_1)\n",
    "compliance_b2 = fill(Diagonal([1.24467e-9, 3.77682e-9, 2.51788e-8, 1.90461e-7,\n",
    "    2.55034e-7, 1.18646e-8]), nelem_b2)\n",
    "\n",
    "# beam 3\n",
    "L_b3 = norm(p4-p3)\n",
    "r_b3 = p3\n",
    "nelem_b3 = 5\n",
    "lengths_b3, xp_b3, xm_b3, Cab_b3 = discretize_beam(L_b3, r_b3, nelem_b3;\n",
    "    frame = Cab_1)\n",
    "compliance_b3 = fill(Diagonal([1.60806e-9, 4.86724e-9, 3.24482e-8, 4.07637e-7,\n",
    "    5.57611e-7, 2.55684e-8]), nelem_b3)\n",
    "\n",
    "# beam 4\n",
    "L_b4 = norm(p5-p4)\n",
    "r_b4 = p4\n",
    "nelem_b4 = 5\n",
    "lengths_b4, xp_b4, xm_b4, Cab_b4 = discretize_beam(L_b4, r_b4, nelem_b4;\n",
    "    frame = Cab_1)\n",
    "compliance_b4 = fill(Diagonal([2.56482e-9, 7.60456e-9, 5.67609e-8, 1.92171e-6,\n",
    "    2.8757e-6, 1.02718e-7]), nelem_b4)\n",
    "\n",
    "# beam 5\n",
    "L_b5 = norm(p6-p5)\n",
    "r_b5 = p5\n",
    "nelem_b5 = 20\n",
    "lengths_b5, xp_b5, xm_b5, Cab_b5 = discretize_beam(L_b5, r_b5, nelem_b5;\n",
    "    frame = Cab_2)\n",
    "compliance_b5 = fill(Diagonal([2.77393e-9, 7.60456e-9, 1.52091e-7, 1.27757e-5,\n",
    "    2.7835e-5, 1.26026e-7]), nelem_b5)\n",
    "\n",
    "# combine elements and points into one array\n",
    "nelem = nelem_b1 + nelem_b2 + nelem_b3 + nelem_b4 + nelem_b5\n",
    "points = vcat(xp_b1, xp_b2[2:end], xp_b3[2:end], xp_b4[2:end], xp_b5[2:end])\n",
    "start = 1:nelem\n",
    "stop = 2:nelem + 1\n",
    "lengths = vcat(lengths_b1, lengths_b2, lengths_b3, lengths_b4, lengths_b5)\n",
    "midpoints = vcat(xm_b1, xm_b2, xm_b3, xm_b4, xm_b5)\n",
    "Cab = vcat(Cab_b1, Cab_b2, Cab_b3, Cab_b4, Cab_b5)\n",
    "compliance = vcat(compliance_b1, compliance_b2, compliance_b3, compliance_b4,\n",
    "    compliance_b5)\n",
    "\n",
    "# create assembly\n",
    "assembly = Assembly(points, start, stop;\n",
    "    compliance = compliance,\n",
    "    frames = Cab,\n",
    "    lengths = lengths,\n",
    "    midpoints = midpoints)\n",
    "\n",
    "Fz = range(0, 70e3, length=141)\n",
    "\n",
    "# pre-allocate memory to reduce run-time\n",
    "ijoint = nelem_b1 + nelem_b2 + nelem_b3 + nelem_b4 + 1\n",
    "prescribed_points = [1, ijoint, nelem+1]\n",
    "static = true\n",
    "system = System(assembly, static; prescribed_points)\n",
    "\n",
    "linear_states = Vector{AssemblyState{Float64}}(undef, length(Fz))\n",
    "for i = 1:length(Fz)\n",
    "\n",
    "    # create dictionary of prescribed conditions\n",
    "    prescribed_conditions = Dict(\n",
    "        # fixed endpoint on beam 1\n",
    "        1 => PrescribedConditions(ux=0, uy=0, uz=0, theta_x=0, theta_y=0,\n",
    "            theta_z=0),\n",
    "\n",
    "        # force applied on point 4\n",
    "        nelem_b1 + nelem_b2 + nelem_b3 + nelem_b4 + 1 => PrescribedConditions(\n",
    "            Fz = Fz[i]),\n",
    "\n",
    "        # fixed endpoint on last beam\n",
    "        nelem+1 => PrescribedConditions(ux=0, uy=0, uz=0, theta_x=0, theta_y=0,\n",
    "            theta_z=0),\n",
    "    )\n",
    "\n",
    "    static_analysis!(system, assembly;\n",
    "        prescribed_conditions = prescribed_conditions,\n",
    "        linear = true)\n",
    "\n",
    "    linear_states[i] = AssemblyState(system, assembly;\n",
    "        prescribed_conditions = prescribed_conditions)\n",
    "\n",
    "end\n",
    "\n",
    "reset_state!(system)\n",
    "nonlinear_states = Vector{AssemblyState{Float64}}(undef, length(Fz))\n",
    "for i = 1:length(Fz)\n",
    "\n",
    "    # create dictionary of prescribed conditions\n",
    "    prescribed_conditions = Dict(\n",
    "        # fixed endpoint on beam 1\n",
    "        1 => PrescribedConditions(ux=0, uy=0, uz=0, theta_x=0, theta_y=0,\n",
    "            theta_z=0),\n",
    "\n",
    "        # force applied on point 4\n",
    "        nelem_b1 + nelem_b2 + nelem_b3 + nelem_b4 + 1 => PrescribedConditions(\n",
    "            Fz = Fz[i]),\n",
    "\n",
    "        # fixed endpoint on last beam\n",
    "        nelem+1 => PrescribedConditions(ux=0, uy=0, uz=0, theta_x=0, theta_y=0,\n",
    "            theta_z=0),\n",
    "    )\n",
    "\n",
    "    static_analysis!(system, assembly;\n",
    "        prescribed_conditions=prescribed_conditions, reset_state=false)\n",
    "\n",
    "    nonlinear_states[i] = AssemblyState(system, assembly;\n",
    "        prescribed_conditions = prescribed_conditions)\n",
    "\n",
    "end\n",
    "\n",
    "reset_state!(system)\n",
    "nonlinear_follower_states = Vector{AssemblyState{Float64}}(undef, length(Fz))\n",
    "for i = 1:length(Fz)\n",
    "    # create dictionary of prescribed conditions\n",
    "    prescribed_conditions = Dict(\n",
    "        # fixed endpoint on beam 1\n",
    "        1 => PrescribedConditions(ux=0, uy=0, uz=0, theta_x=0, theta_y=0,\n",
    "            theta_z=0),\n",
    "\n",
    "        # force applied on point 4\n",
    "        nelem_b1 + nelem_b2 + nelem_b3 + nelem_b4 + 1 => PrescribedConditions(\n",
    "            Fz_follower = Fz[i]),\n",
    "\n",
    "        # fixed endpoint on last beam\n",
    "        nelem+1 => PrescribedConditions(ux=0, uy=0, uz=0, theta_x=0, theta_y=0,\n",
    "            theta_z=0),\n",
    "    )\n",
    "\n",
    "    static_analysis!(system, assembly;\n",
    "        prescribed_conditions=prescribed_conditions, reset_state=false)\n",
    "\n",
    "    nonlinear_follower_states[i] = AssemblyState(system, assembly;\n",
    "        prescribed_conditions = prescribed_conditions)\n",
    "\n",
    "end\n",
    "\n",
    "nothing #hide"
   ],
   "metadata": {},
   "execution_count": 1
  },
  {
   "cell_type": "markdown",
   "source": [
    "Note that we incrementally increased the load from 0 to 70 kN in order to ensure that we\n",
    "obtained converged solutions."
   ],
   "metadata": {}
  },
  {
   "cell_type": "markdown",
   "source": [
    "To visualize the differences between the different types of analyses we can plot the\n",
    "load deflection curve."
   ],
   "metadata": {}
  },
  {
   "outputs": [],
   "cell_type": "code",
   "source": [
    "using Plots\n",
    "pyplot()\n",
    "\n",
    "plot(\n",
    "    xlim = (0, 7),\n",
    "    xticks = 0:1:7,\n",
    "    xlabel = \"Vertical Displacement at the Joint (m)\",\n",
    "    yticks = 0:10:70,\n",
    "    ylim = (0, 70),\n",
    "    ylabel = \"Load (kN)\",\n",
    "    grid = false,\n",
    "    overwrite_figure=false\n",
    "    )\n",
    "uz_l = [linear_states[i].points[ijoint].u[3] for i = 1:length(Fz)]\n",
    "uz_nl = [nonlinear_states[i].points[ijoint].u[3] for i = 1:length(Fz)]\n",
    "uz_fnl = [nonlinear_follower_states[i].points[ijoint].u[3] for i = 1:length(Fz)]\n",
    "\n",
    "plot!(uz_l, Fz./1e3, label=\"Linear\")\n",
    "plot!(uz_nl, Fz./1e3, label=\"Nonlinear with Dead Force\")\n",
    "plot!(uz_fnl, Fz./1e3, label=\"Nonlinear with Follower Force\")\n",
    "plot!(show=true)\n",
    "nothing #hide"
   ],
   "metadata": {},
   "execution_count": 2
  },
  {
   "cell_type": "markdown",
   "source": [
    "This plot matches the plot provided by Wenbin Yu in \"GEBT: A general-purpose nonlinear\n",
    "analysis tool for composite beams\"."
   ],
   "metadata": {}
  },
  {
   "cell_type": "markdown",
   "source": [
    "We can also visualize the deformed geometry and inspect the associated point and element\n",
    "data for any of these operating conditions conditions using ParaView.  To demonstrate\n",
    "we will visualize the 70kN follower force condition and set the color gradient to\n",
    "match the magnitude of the deflections."
   ],
   "metadata": {}
  },
  {
   "outputs": [],
   "cell_type": "code",
   "source": [
    "airfoil  = [ #FX 60-100 airfoil\n",
    "    0.0000000 0.0000000;\n",
    "    0.0010700 0.0057400;\n",
    "    0.0042800 0.0114400;\n",
    "    0.0096100 0.0177500;\n",
    "    0.0170400 0.0236800;\n",
    "    0.0265300 0.0294800;\n",
    "    0.0380600 0.0352300;\n",
    "    0.0515600 0.0405600;\n",
    "    0.0669900 0.0460900;\n",
    "    0.0842700 0.0508600;\n",
    "    0.1033200 0.0556900;\n",
    "    0.1240800 0.0598900;\n",
    "    0.1464500 0.0640400;\n",
    "    0.1703300 0.0675400;\n",
    "    0.1956200 0.0708100;\n",
    "    0.2222100 0.0733900;\n",
    "    0.2500000 0.0756500;\n",
    "    0.2788600 0.0772000;\n",
    "    0.3086600 0.0783800;\n",
    "    0.3392800 0.0788800;\n",
    "    0.3705900 0.0789800;\n",
    "    0.4024500 0.0784500;\n",
    "    0.4347400 0.0775000;\n",
    "    0.4673000 0.0759600;\n",
    "    0.5000000 0.0740900;\n",
    "    0.5327000 0.0717400;\n",
    "    0.5652600 0.0691100;\n",
    "    0.5975500 0.0660800;\n",
    "    0.6294100 0.0627500;\n",
    "    0.6607200 0.0590500;\n",
    "    0.6913400 0.0551100;\n",
    "    0.7211400 0.0508900;\n",
    "    0.7500000 0.0465200;\n",
    "    0.7777900 0.0420000;\n",
    "    0.8043801 0.0374700;\n",
    "    0.8296700 0.0329800;\n",
    "    0.8535500 0.0286400;\n",
    "    0.8759201 0.0244700;\n",
    "    0.8966800 0.0205300;\n",
    "    0.9157300 0.0168100;\n",
    "    0.9330100 0.0134200;\n",
    "    0.9484400 0.0103500;\n",
    "    0.9619400 0.0076600;\n",
    "    0.9734700 0.0053400;\n",
    "    0.9829600 0.0034100;\n",
    "    0.9903900 0.0019300;\n",
    "    0.9957200 0.0008600;\n",
    "    0.9989300 0.0002300;\n",
    "    1.0000000 0.0000000;\n",
    "    0.9989300 0.0001500;\n",
    "    0.9957200 0.0007000;\n",
    "    0.9903900 0.0015100;\n",
    "    0.9829600 0.00251;\n",
    "    0.9734700 0.00377;\n",
    "    0.9619400 0.00515;\n",
    "    0.9484400 0.00659;\n",
    "    0.9330100 0.00802;\n",
    "    0.9157300 0.00941;\n",
    "    0.8966800 0.01072;\n",
    "    0.8759201 0.01186;\n",
    "    0.8535500 0.0128;\n",
    "    0.8296700 0.01347;\n",
    "    0.8043801 0.01381;\n",
    "    0.7777900 0.01373;\n",
    "    0.7500000 0.01329;\n",
    "    0.7211400 0.01241;\n",
    "    0.6913400 0.01118;\n",
    "    0.6607200 0.00951;\n",
    "    0.6294100 0.00748;\n",
    "    0.5975500 0.00496;\n",
    "    0.5652600 0.00217;\n",
    "    0.532700  -0.00092;\n",
    "    0.500000  -0.00405;\n",
    "    0.467300  -0.00731;\n",
    "    0.434740  -0.01045;\n",
    "    0.402450  -0.01357;\n",
    "    0.370590  -0.01637;\n",
    "    0.339280  -0.01895;\n",
    "    0.308660  -0.021;\n",
    "    0.278860  -0.02275;\n",
    "    0.250000  -0.02389;\n",
    "    0.222210  -0.02475;\n",
    "    0.195620  -0.025;\n",
    "    0.170330  -0.02503;\n",
    "    0.146450  -0.02447;\n",
    "    0.124080  -0.02377;\n",
    "    0.103320  -0.02246;\n",
    "    0.084270  -0.0211;\n",
    "    0.066990  -0.01913;\n",
    "    0.051560  -0.0173;\n",
    "    0.038060  -0.01481;\n",
    "    0.026530  -0.01247;\n",
    "    0.017040  -0.0097;\n",
    "    0.009610  -0.00691;\n",
    "    0.004280  -0.00436;\n",
    "    0.001070  -0.002;\n",
    "    0.0        0.0;\n",
    "]\n",
    "\n",
    "section = zeros(3, size(airfoil, 1))\n",
    "for ic = 1:size(airfoil, 1)\n",
    "    section[1,ic] = airfoil[ic,1] - 0.5\n",
    "    section[2,ic] = 0\n",
    "    section[3,ic] = airfoil[ic,2]\n",
    "end\n",
    "\n",
    "write_vtk(\"static-joined-wing-visualization\", assembly, nonlinear_follower_states[end];\n",
    "    sections = section)"
   ],
   "metadata": {},
   "execution_count": 3
  },
  {
   "cell_type": "markdown",
   "source": [
    "![](../assets/static-joined-wing-visualization.png)"
   ],
   "metadata": {}
  },
  {
   "cell_type": "markdown",
   "source": [
    "---\n",
    "\n",
    "*This notebook was generated using [Literate.jl](https://github.com/fredrikekre/Literate.jl).*"
   ],
   "metadata": {}
  }
 ],
 "nbformat_minor": 3,
 "metadata": {
  "language_info": {
   "file_extension": ".jl",
   "mimetype": "application/julia",
   "name": "julia",
   "version": "1.6.5"
  },
  "kernelspec": {
   "name": "julia-1.6",
   "display_name": "Julia 1.6.5",
   "language": "julia"
  }
 },
 "nbformat": 4
}
