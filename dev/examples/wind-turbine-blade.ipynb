{
 "cells": [
  {
   "cell_type": "markdown",
   "source": [
    "# Time-Domain Simulation of a Wind Turbine Blade"
   ],
   "metadata": {}
  },
  {
   "outputs": [],
   "cell_type": "code",
   "source": [
    "using GXBeam, LinearAlgebra\n",
    "\n",
    "L = 60 # m\n",
    "\n",
    "# create points\n",
    "nelem = 10\n",
    "x = range(0, L, length=nelem+1)\n",
    "y = zero(x)\n",
    "z = zero(x)\n",
    "points = [[x[i],y[i],z[i]] for i = 1:length(x)]\n",
    "\n",
    "# index of endpoints of each beam element\n",
    "start = 1:nelem\n",
    "stop = 2:nelem+1\n",
    "\n",
    "# stiffness matrix for each beam element\n",
    "stiffness = fill(\n",
    "    [2.389e9  1.524e6  6.734e6 -3.382e7 -2.627e7 -4.736e8\n",
    "     1.524e6  4.334e8 -3.741e6 -2.935e5  1.527e7  3.835e5\n",
    "     6.734e6 -3.741e6  2.743e7 -4.592e5 -6.869e5 -4.742e6\n",
    "    -3.382e7 -2.935e5 -4.592e5  2.167e7 -6.279e5  1.430e6\n",
    "    -2.627e7  1.527e7 -6.869e5 -6.279e5  1.970e7  1.209e7\n",
    "    -4.736e8  3.835e5 -4.742e6  1.430e6  1.209e7  4.406e8],\n",
    "    nelem)\n",
    "\n",
    "# mass matrix for each beam element\n",
    "mass = fill(\n",
    "    [258.053      0.0        0.0      0.0      7.07839  -71.6871\n",
    "       0.0      258.053      0.0     -7.07839  0.0        0.0\n",
    "       0.0        0.0      258.053   71.6871   0.0        0.0\n",
    "       0.0       -7.07839   71.6871  48.59     0.0        0.0\n",
    "       7.07839    0.0        0.0      0.0      2.172      0.0\n",
    "     -71.6871     0.0        0.0      0.0      0.0       46.418],\n",
    "     nelem)\n",
    "\n",
    "# create assembly of interconnected nonlinear beams\n",
    "assembly = Assembly(points, start, stop; stiffness=stiffness, mass=mass)\n",
    "\n",
    "# prescribed conditions\n",
    "prescribed_conditions = (t) -> begin\n",
    "    Dict(\n",
    "        # fixed left side\n",
    "        1 => PrescribedConditions(ux=0, uy=0, uz=0, theta_x=0, theta_y=0, theta_z=0),\n",
    "        # force on right side\n",
    "        nelem+1 => PrescribedConditions(Fz = 1e5*sin(20*t))\n",
    "    )\n",
    "end\n",
    "\n",
    "# simulation time\n",
    "t = 0:0.001:2.0\n",
    "\n",
    "system, history, converged = time_domain_analysis(assembly, t;\n",
    "    prescribed_conditions = prescribed_conditions)\n",
    "\n",
    "nothing #hide"
   ],
   "metadata": {},
   "execution_count": 1
  },
  {
   "cell_type": "markdown",
   "source": [
    "We can visualize tip displacements and the resultant forces in the root by accessing\n",
    "the post-processed results for each time step contained in the variable `history`.\n",
    "Note that the root resultant forces for this case are equal to the external\n",
    "forces/moments, but with opposite sign."
   ],
   "metadata": {}
  },
  {
   "outputs": [],
   "cell_type": "code",
   "source": [
    "using Plots\n",
    "pyplot()\n",
    "\n",
    "point = vcat(fill(nelem+1, 6), fill(1, 6))\n",
    "field = [:u, :u, :u, :theta, :theta, :theta, :F, :F, :F, :M, :M, :M]\n",
    "direction = [1, 2, 3, 1, 2, 3, 1, 2, 3, 1, 2, 3]\n",
    "ylabel = [\"\\$u_x\\$ (\\$m\\$)\", \"\\$u_y\\$ (\\$m\\$)\", \"\\$u_z\\$ (\\$m\\$)\",\n",
    "    \"Rodriguez Parameter \\$\\\\theta_x\\$ (degree)\",\n",
    "    \"Rodriguez Parameter \\$\\\\theta_y\\$ (degree)\",\n",
    "    \"Rodriguez Parameter \\$\\\\theta_z\\$ (degree)\",\n",
    "    \"\\$F_x\\$ (\\$N\\$)\", \"\\$F_y\\$ (\\$N\\$)\", \"\\$F_z\\$ (\\$N\\$)\",\n",
    "    \"\\$M_x\\$ (\\$Nm\\$)\", \"\\$M_y\\$ (\\$Nm\\$)\", \"\\$M_z\\$ (\\$N\\$)\"]\n",
    "\n",
    "for i = 1:12\n",
    "\n",
    "    local y\n",
    "\n",
    "    plot(\n",
    "        xlim = (0, 2.0),\n",
    "        xticks = 0:0.5:2.0,\n",
    "        xlabel = \"Time (s)\",\n",
    "        ylabel = ylabel[i],\n",
    "        grid = false,\n",
    "        overwrite_figure=false\n",
    "        )\n",
    "    y = [getproperty(state.points[point[i]], field[i])[direction[i]]\n",
    "        for state in history]\n",
    "\n",
    "    if field[i] == :theta\n",
    "        # convert to Rodriguez parameter\n",
    "        @. y = 4*atan(y/4)\n",
    "        # convert to degrees\n",
    "        @. y = rad2deg(y)\n",
    "    end\n",
    "\n",
    "    if field[i] == :F || field[i] == :M\n",
    "        y = -y\n",
    "    end\n",
    "\n",
    "    plot!(t, y, label=\"\")\n",
    "    plot!(show=true)\n",
    "end\n",
    "\n",
    "nothing #hide"
   ],
   "metadata": {},
   "execution_count": 2
  },
  {
   "cell_type": "markdown",
   "source": [
    "These plots are identical to those presented by Qi Wang, Wenbin Yu, and Michael A.\n",
    "Sprague in \"Geometric Nonlinear Analysis of Composite Beams Using Wiener-Milenkovic\n",
    "Parameters\"."
   ],
   "metadata": {}
  },
  {
   "cell_type": "markdown",
   "source": [
    "We can also visualize the time history of the system using ParaView."
   ],
   "metadata": {}
  },
  {
   "outputs": [],
   "cell_type": "code",
   "source": [
    "root_chord = 1.9000\n",
    "tip_chord =  0.4540\n",
    "airfoil = [ # MH-104\n",
    "    1.00000000  0.00000000;\n",
    "    0.99619582  0.00017047;\n",
    "    0.98515158  0.00100213;\n",
    "    0.96764209  0.00285474;\n",
    "    0.94421447  0.00556001;\n",
    "    0.91510964  0.00906779;\n",
    "    0.88074158  0.01357364;\n",
    "    0.84177999  0.01916802;\n",
    "    0.79894110  0.02580144;\n",
    "    0.75297076  0.03334313;\n",
    "    0.70461763  0.04158593;\n",
    "    0.65461515  0.05026338;\n",
    "    0.60366461  0.05906756;\n",
    "    0.55242353  0.06766426;\n",
    "    0.50149950  0.07571157;\n",
    "    0.45144530  0.08287416;\n",
    "    0.40276150  0.08882939;\n",
    "    0.35589801  0.09329359;\n",
    "    0.31131449  0.09592864;\n",
    "    0.26917194  0.09626763;\n",
    "    0.22927064  0.09424396;\n",
    "    0.19167283  0.09023579;\n",
    "    0.15672257  0.08451656;\n",
    "    0.12469599  0.07727756;\n",
    "    0.09585870  0.06875796;\n",
    "    0.07046974  0.05918984;\n",
    "    0.04874337  0.04880096;\n",
    "    0.03081405  0.03786904;\n",
    "    0.01681379  0.02676332;\n",
    "    0.00687971  0.01592385;\n",
    "    0.00143518  0.00647946;\n",
    "    0.00053606  0.00370956;\n",
    "    0.00006572  0.00112514;\n",
    "    0.00001249 -0.00046881;\n",
    "    0.00023032 -0.00191488;\n",
    "    0.00079945 -0.00329201;\n",
    "    0.00170287 -0.00470585;\n",
    "    0.00354717 -0.00688469;\n",
    "    0.00592084 -0.00912202;\n",
    "    0.01810144 -0.01720842;\n",
    "    0.03471169 -0.02488211;\n",
    "    0.05589286 -0.03226730;\n",
    "    0.08132751 -0.03908459;\n",
    "    0.11073805 -0.04503763;\n",
    "    0.14391397 -0.04986836;\n",
    "    0.18067874 -0.05338180;\n",
    "    0.22089879 -0.05551392;\n",
    "    0.26433734 -0.05636585;\n",
    "    0.31062190 -0.05605816;\n",
    "    0.35933893 -0.05472399;\n",
    "    0.40999990 -0.05254383;\n",
    "    0.46204424 -0.04969990;\n",
    "    0.51483073 -0.04637175;\n",
    "    0.56767889 -0.04264894;\n",
    "    0.61998250 -0.03859653;\n",
    "    0.67114514 -0.03433153;\n",
    "    0.72054815 -0.02996944;\n",
    "    0.76758733 -0.02560890;\n",
    "    0.81168064 -0.02134397;\n",
    "    0.85227225 -0.01726049;\n",
    "    0.88883823 -0.01343567;\n",
    "    0.92088961 -0.00993849;\n",
    "    0.94797259 -0.00679919;\n",
    "    0.96977487 -0.00402321;\n",
    "    0.98607009 -0.00180118;\n",
    "    0.99640466 -0.00044469;\n",
    "    1.00000000  0.00000000;\n",
    "]\n",
    "\n",
    "sections = zeros(3, size(airfoil, 1), length(points))\n",
    "for ip = 1:length(points)\n",
    "    chord = root_chord * (1 - x[ip]/L) + tip_chord * x[ip]/L\n",
    "    sections[1, :, ip] .= 0\n",
    "    sections[2, :, ip] .= chord .* (airfoil[:,1] .- 0.5)\n",
    "    sections[3, :, ip] .= chord .* airfoil[:,2]\n",
    "end\n",
    "\n",
    "write_vtk(\"wind-turbine-blade-simulation\", assembly, history, t; sections = sections)"
   ],
   "metadata": {},
   "execution_count": 3
  },
  {
   "cell_type": "markdown",
   "source": [
    "![](../assets/wind-turbine-blade-simulation.gif)"
   ],
   "metadata": {}
  },
  {
   "cell_type": "markdown",
   "source": [
    "---\n",
    "\n",
    "*This notebook was generated using [Literate.jl](https://github.com/fredrikekre/Literate.jl).*"
   ],
   "metadata": {}
  }
 ],
 "nbformat_minor": 3,
 "metadata": {
  "language_info": {
   "file_extension": ".jl",
   "mimetype": "application/julia",
   "name": "julia",
   "version": "1.6.5"
  },
  "kernelspec": {
   "name": "julia-1.6",
   "display_name": "Julia 1.6.5",
   "language": "julia"
  }
 },
 "nbformat": 4
}
